todo Add swarm tests for top scenarios, including the scalar at root level scenario.
todo Create a new language, similar to this, except using pattern matching instead of prefix notation.
anyCell
keywordCell
 highlightScope keyword
stringCell
 highlightScope string
booleanCell
 enum true false
 highlightScope constant.numeric
numberCell
 highlightScope constant.numeric
dugNode
 root
 description A demonstration prefix Tree Language that compiles to JSON.
 inScope valueNode
 catchAllNodeType errorNode
 javascript
  compile() {
   const res = super.compile()
   return JSON.stringify(JSON.parse(res), null, 2)
  }
valueNode
 cells keywordCell
nullNode
 compiler
  stringTemplate null
 extends valueNode
 match null
numberNode
 match number
 extends valueNode
 cells keywordCell numberCell
 compiler
  stringTemplate {numberCell}
stringNode
 match string
 catchAllCellType stringCell
 compiler
  stringTemplate "{stringCell}"
 extends valueNode
booleanNode
 match boolean
 extends valueNode
 cells keywordCell booleanCell
 compiler
  stringTemplate {booleanCell}
objectNode
 match object
 catchAllNodeType memberNode
 extends valueNode
 compiler
  stringTemplate  
  joinChildrenWith , 
  openChildren {
  closeChildren }
arrayNode
 match array
 extends valueNode
 inScope valueNode
 compiler
  stringTemplate  
  joinChildrenWith , 
  openChildren [
  closeChildren ]
memberNode
 inScope valueNode
 compiler
  stringTemplate "{stringCell}" :
 cells stringCell
errorNode
 baseNodeType errorNode