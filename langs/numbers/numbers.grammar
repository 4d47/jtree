floatCell
commentCell
 highlightScope comment
keywordCell
commentKeywordCell
 extends keywordCell
 highlightScope comment
errorCell
 highlightScope invalid
numberCell
 highlightScope constant.numeric
 extends floatCell
numbersCell
 extends numberCell
operatorCell
 highlightScope keyword.operator.arithmetic
numbersNode
 root
 description A useless Tree Language for testing Tree Notation features.
 inScope abstractArithmeticReducerNode commentNode hashBangNode
 catchAllNodeType errorNode
abstractArithmeticReducerNode
 description First reduces any child lists to one number and then reduces its own lists to one number using provided operator.
 javascript
  executeSync() {
   return this.numbersCell.slice(1).reduce((curr, tot) => eval(`${curr}${this.operator}${tot}`), this.numbersCell[0])
  }
  async execute() {
   return this.executeSync()
  }
 inScope abstractArithmeticReducerNode commentNode
 abstract
 cells operatorCell
 catchAllCellType numbersCell
modNode
 match %
 extends abstractArithmeticReducerNode
 string operator %
timesNode
 match *
 extends abstractArithmeticReducerNode
 string operator *
addNode
 match +
 extends abstractArithmeticReducerNode
 string operator +
substractNode
 match -
 extends abstractArithmeticReducerNode
 string operator -
divideNode
 match /
 extends abstractArithmeticReducerNode
 string operator /
commentNode
 description This is a line comment.
 catchAllCellType commentCell
 catchAllNodeType commentNode
 cells commentKeywordCell
simpleCommentNode
 match comment
 extends commentNode
hashBangNode
 match #!
 extends commentNode
errorNode
 catchAllCellType errorCell
 baseNodeType errorNode
 cells errorCell