anyCell
columnNameEnumCell
columnNameCell
errorCell
 highlightScope invalid
intCell
 highlightScope constant.numeric
onoffCell
 enum on off
wordCell
topLevelPropertyCell
 highlightScope constant.language
opSymbolCell
 highlightScope keyword.operator.arithmetic
jibberishNode
 root
 description A useless Tree Language built for testing Tree Notation code.
 javascript
  executeSync() {
   return 42
  }
 compilesTo txt
 catchAllNodeType errorNode
 inScope topLevelNode textNode someAbstractClassNode
someAbstractClassNode
 abstract
extendsAbstractNode
 cells topLevelPropertyCell intCell
 extends someAbstractClassNode
topLevelNode
 abstract
 cells topLevelPropertyCell
colorPropertiesNode
 cells topLevelPropertyCell intCell
 extends topLevelNode
 abstract
hueNode
 extends colorPropertiesNode
saturationNode
 extends colorPropertiesNode
constrastNode
 extends colorPropertiesNode
abstractHtmlNode
 inScope contentNode
 extends topLevelNode
 abstract
h1Node
 crux html.h1
 extends abstractHtmlNode
addNode
 extends topLevelNode
plusNode
 crux +
 extends addNode
 example Adding two numbers:
  + 1 2
 catchAllCellType intCell
 cells opSymbolCell
blockNode
 inScope topLevelNode scoreBlockNode
 extends topLevelNode
scoreBlockNode
 description Test that inscope extends and does not overwrite.
 extends blockNode
 inScope scoresNode
toNode
 cells topLevelPropertyCell wordCell
 compiler
  stringTemplate to {word}
  closeChildren end
 extends blockNode
fooNode
 extends topLevelNode
xColumnNameNode
 description The name of the column to use for the x axis
 cells topLevelPropertyCell columnNameEnumCell
 javascript
  getRunTimeEnumOptions(cell) {
   return cell.getCellTypeId() === "columnNameEnumCell" ? ["gender", "height", "weight"] : undefined
  }
 extends topLevelNode
lightbulbStateNode
 cells topLevelPropertyCell onoffCell
 extends topLevelNode
nestedNode
 extends topLevelNode
nodeWithConstsNode
 string greeting hello world
 string singleCell hello
 string thisHasQuotes "'`
 string longText
  hello
  world
 int score1 28
 int anArray 2 3 4
 float score2 3.01
 boolean win true
 extends topLevelNode
nodeExpandsConstsNode
 string greeting hola
 extends nodeWithConstsNode
someCodeNode
 catchAllNodeType lineOfCodeNode
 extends topLevelNode
typeNode
 cells topLevelPropertyCell wordCell
 single
 extends topLevelNode
contentNode
 baseNodeType blobNode
errorNode
 catchAllCellType errorCell
 baseNodeType errorNode
 cells errorCell
lineOfCodeNode
 catchAllCellType wordCell
textNode
 baseNodeType blobNode
scoresNode
 catchAllCellType intCell
 cells topLevelPropertyCell