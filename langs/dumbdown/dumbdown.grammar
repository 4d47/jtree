dumbdownNode
 extensions dd dumbdown
 description A Tree Language that compiles to HTML. An alternative to Markdown.
 root
 inScope abstractTopLevelNode
 catchAllNodeType blankLineNode
 compilesTo html
 example
  title Hello world
  subtitle This is dumbdown
  
  paragraph It compiles to HTML. Blank lines get turned into brs.
  link https://treenotation.org dumbdown is a Tree Language.
  list
   - It has lists
   - Too!
  code
   // You can add code as well.
   print("Hello world")
abstractTopLevelNode
 abstract
 cells keywordCell
anyCell
dashCell
 highlightScope constant.language
codeCell
 highlightScope comment
keywordCell
 highlightScope keyword
textCell
 highlightScope string
urlCell
 highlightScope constant.language
blankLineNode
 description Blank lines compile to a br in the HTML.
 compiler
  stringTemplate <br>
titleNode
 catchAllCellType textCell
 extends abstractTopLevelNode
 compiler
  stringTemplate <h1>{textCell}</h1>
linkNode
 cells keywordCell urlCell
 catchAllCellType textCell
 extends abstractTopLevelNode
 compiler
  stringTemplate <a href="{urlCell}">{textCell}</a>
paragraphNode
 inScope linkNode
 catchAllCellType textCell
 extends abstractTopLevelNode
 compiler
  stringTemplate <p>{textCell}</p>
subtitleNode
 catchAllCellType textCell
 extends abstractTopLevelNode
 compiler
  stringTemplate <h2>{textCell}</h2>
lineOfCodeNode
 catchAllCellType codeCell
 cells codeCell
codeNode
 description A code block.
 catchAllNodeType lineOfCodeNode
 extends abstractTopLevelNode
 todo Fix spacing
 compiler
  openChildren <code>
  closeChildren </code>
  stringTemplate 
listNode
 inScope dashNode
 extends abstractTopLevelNode
 compiler
  stringTemplate 
  openChildren <ul>
  closeChildren </ul>
dashNode
 match -
 catchAllCellType textCell
 compiler
  stringTemplate <li>{textCell}</li>
 cells dashCell