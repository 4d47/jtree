grammar
 name grammar
 description This file is just used for type checking and documenting grammar files. The actual tree grammar parser is hard coded.
 catchAllKeyword _error
 keywordOrder grammar cellType abstract keyword
 extensions grammar gram
 example A grammar that parses anything:
  grammar
   name latin
   catchAllKeyword any
  keyword any
   any
 version 1.1.0
 keywords
  grammar
  cellType
  abstract
  keyword
cellType anyWord
cellType builtInConstructorOrConstructorName
cellType builtInConstructorOrFilePath
cellType cellTypeEnum
 keywordTable cellType 1
cellType cellTypeName
cellType compilerTargetExtension
 description The target extension of the compiler output such as "o" or "js" or "html"
cellType constructorName
cellType enumOption
cellType extendedKeyword
cellType fileExtension
cellType filepath
cellType float
 regex \-?[0-9]*\.?[0-9]*
cellType grammarName
 highlightScope constant
cellType int
 regex \-?[0-9]+
 highlightScope constant.numeric
cellType javascriptCodeWord
cellType keywordName
 highlightScope variable.parameter
cellType lowercaseWord
 regex [a-z]+
cellType regexWord
 highlightScope string.regexp
cellType scopeName
 enum comment comment.block comment.block.documentation comment.line constant constant.character.escape constant.language constant.numeric constant.numeric.complex constant.numeric.complex.imaginary constant.numeric.complex.real constant.numeric.float constant.numeric.float.binary constant.numeric.float.decimal constant.numeric.float.hexadecimal constant.numeric.float.octal constant.numeric.float.other constant.numeric.integer constant.numeric.integer.binary constant.numeric.integer.decimal constant.numeric.integer.hexadecimal constant.numeric.integer.octal constant.numeric.integer.other constant.other constant.other.placeholder entity entity.name entity.name.class entity.name.class.forward-decl entity.name.constant entity.name.enum entity.name.function entity.name.function.constructor entity.name.function.destructor entity.name.impl entity.name.interface entity.name.label entity.name.namespace entity.name.section entity.name.struct entity.name.tag entity.name.trait entity.name.type entity.name.union entity.other.attribute-name entity.other.inherited-class invalid invalid.deprecated invalid.illegal keyword keyword.control keyword.control.conditional keyword.control.import keyword.declaration keyword.operator keyword.operator.arithmetic keyword.operator.assignment keyword.operator.bitwise keyword.operator.logical keyword.operator.word keyword.other markup markup.bold markup.deleted markup.heading markup.inserted markup.italic markup.list.numbered markup.list.unnumbered markup.other markup.quote markup.raw.block markup.raw.inline markup.underline markup.underline.link meta meta.annotation meta.annotation.identifier meta.annotation.parameters meta.block meta.braces meta.brackets meta.class meta.enum meta.function meta.function-call meta.function.parameters meta.function.return-type meta.generic meta.group meta.impl meta.interface meta.interpolation meta.namespace meta.paragraph meta.parens meta.path meta.preprocessor meta.string meta.struct meta.tag meta.toc-list meta.trait meta.type meta.union punctuation punctuation.accessor punctuation.definition.annotation punctuation.definition.comment punctuation.definition.generic.begin punctuation.definition.generic.end punctuation.definition.keyword punctuation.definition.string.begin punctuation.definition.string.end punctuation.definition.variable punctuation.section.block.begin punctuation.section.block.end punctuation.section.braces.begin punctuation.section.braces.end punctuation.section.brackets.begin punctuation.section.brackets.end punctuation.section.group.begin punctuation.section.group.end punctuation.section.interpolation.begin punctuation.section.interpolation.end punctuation.section.parens.begin punctuation.section.parens.end punctuation.separator punctuation.separator.continuation punctuation.terminator source source.language-suffix.embedded storage storage.modifier storage.type storage.type keyword.declaration.type storage.type.class keyword.declaration.class storage.type.enum keyword.declaration.enum storage.type.function keyword.declaration.function storage.type.impl keyword.declaration.impl storage.type.interface keyword.declaration.interface storage.type.struct keyword.declaration.struct storage.type.trait keyword.declaration.trait storage.type.union keyword.declaration.union string string.quoted.double string.quoted.other string.quoted.single string.quoted.triple string.regexp string.unquoted support support.class support.constant support.function support.module support.type text text.html text.xml variable variable.annotation variable.function variable.language variable.other variable.other.constant variable.other.member variable.other.readwrite variable.parameter
 highlightScope string
cellType semanticVersion
 regex [0-9]+\.[0-9]+\.[0-9]+
 highlightScope constant.numeric
cellType string
 highlightScope string
cellType tag
cellType word
 regex [a-zA-Z]+
 highlightScope variable.parameter
cellType extendedCellType cellTypeName
cellType exampleWord string
 highlightScope comment
abstract _compilerRule
 catchAllCellType anyWord
 highlightScope entity.other.attribute-name
abstract _environment
abstract _keywordRule
 highlightScope entity.other.attribute-name
 single
abstract _nonTerminalKeywordRule _keywordRule
keyword _constantAny
 catchAllCellType anyWord
 catchAllKeyword _constantAny
keyword _constantDefinition
 catchAllCellType anyWord
 catchAllKeyword _constantAny
keyword _defaultTuple
 cells anyWord
 catchAllCellType anyWord
keyword _error
 constructors
  nodejs ErrorNode
  browser ErrorNode
keyword _exampleLine
 catchAllCellType exampleWord
 highlightScope comment
 catchAllKeyword _exampleLine
keyword _keywordUse
keyword cellType
 cells cellTypeName
 catchAllCellType extendedCellType
 highlightScope variable.parameter
 keywords
  highlightScope
  regex
  keywordTable
  description
  enum
  parseWith
keyword enum
 catchAllCellType enumOption
 highlightScope entity.other.attribute-name
keyword extensions
 catchAllCellType fileExtension
 highlightScope constant.language
 description File extensions of your language.
keyword grammar
 required
 single
 highlightScope constant.language
 keywords
  name
  description
  example
  extensions
  keywordOrder
  keywords
  constructors
  compiler
  catchAllKeyword
  version
keyword group
 catchAllCellType keywordName
 highlightScope entity.other.attribute-name
 description Available on abstract nodes only. Use it to more concisely generate keywords all extending from this abstract keyword, if they have no special properties.
keyword javascriptCodeLine
 catchAllCellType javascriptCodeWord
 catchAllKeyword javascriptCodeLine
keyword keyword
 cells keywordName
 catchAllCellType extendedKeyword
 catchAllKeyword _error
 constructors
  nodejs NonTerminalNode
  browser NonTerminalNode
 highlightScope variable.function
 keywords
  _keywordRule
keyword keywordOrder
 catchAllCellType keywordName
 highlightScope constant.language
 description Optionally specify what order keywords should be sorted in when prettifying a file.
keyword keywordTable
 cells anyWord int
 highlightScope entity.other.attribute-name
keyword name
 cells grammarName
 highlightScope constant.language
 description The name of your language. Also serves as the default file extension.
keyword parseWith
 catchAllCellType anyWord
 highlightScope entity.other.attribute-name
 single
keyword regex
 catchAllCellType regexWord
 description The word must match this pattern or it shall be marked as an error.
 highlightScope entity.other.attribute-name
 single
keyword version
 cells semanticVersion
 highlightScope constant.language
 description Version number of your language.
keyword closeChildren _compilerRule
keyword indentCharacter _compilerRule
keyword listDelimiter _compilerRule
keyword openChildren _compilerRule
keyword sub _compilerRule
keyword browser _environment
 cells builtInConstructorOrConstructorName
 description builtInConstructorOrConstructorName will be searched for on the window object.
keyword javascript _environment
 catchAllCellType javascriptCodeWord
 catchAllKeyword javascriptCodeLine
 description Put in some code to eval.
keyword nodejs _environment
 cells builtInConstructorOrFilePath
 catchAllCellType constructorName
 description For nodejs constructors, if specified, the existence of this constructor will be checked when the grammar file is checked for errors.
keyword any _keywordRule
keyword catchAllCellType _keywordRule
 cells cellTypeEnum
 description If there are extra words in the node's line, parse these words as this type.
keyword catchAllKeyword _keywordRule
 description If a keyword is not found in the keywords list, instantiate this type of node instead.
 cells keywordName
keyword cells _keywordRule
 catchAllCellType cellTypeEnum
 description Describes the word type of each word in the line.
keyword compiler _keywordRule
 cells compilerTargetExtension
 keywords
  sub
  listDelimiter
  openChildren
  closeChildren
  indentCharacter
keyword constants _keywordRule
 catchAllKeyword _constantDefinition
keyword defaults _keywordRule
 catchAllKeyword _defaultTuple
keyword description _keywordRule
 catchAllCellType string
keyword example _keywordRule
 description Provide a one line description and then a snippet of example code.
 catchAllCellType exampleWord
 catchAllKeyword _exampleLine
 highlightScope comment
keyword frequency _keywordRule
 cells float
keyword highlightScope _keywordRule
 cells scopeName
 description Provide this to get syntax highlighting in editors like Sublime and CodeMirror. These are all listed here https://www.sublimetext.com/docs/3/scope_naming.html
keyword required _keywordRule
 description If present, the parent node will have an error if one of these nodes is not provided.
keyword single _keywordRule
 description If present and their are more than 1 of these nodes on the parent, an error will be present.
keyword tags _keywordRule
 catchAllCellType tag
keyword constructors _nonTerminalKeywordRule
 keywords
  _environment
 description A map of language/environment to filepaths and constructor classes to instantiate nodes with.
keyword keywords _nonTerminalKeywordRule
 catchAllKeyword _keywordUse
 description A list of possible child keywords for a node. This is a not a leaf node in order to allow seamless extending.
keyword abstract keyword
 highlightScope entity.name.tag
 description An abstract keyword cannot be used by itself but provides common functionality to keywords that extend it.
 keywords
  _keywordRule
  group