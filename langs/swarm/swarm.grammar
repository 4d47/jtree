grammar
 name swarm
 description A Tree Language for unit testing.
 nodeTypes
  #!
  #setup
  testBlock
 catchAllNodeType error
 constructors
  nodejs ./SwarmProgramRoot.js
cellType anyFirstWord
cellType anyWord
 highlightScope string
cellType commentWord
 highlightScope string
cellType filepath
 highlightScope string
cellType topWord
 highlightScope constant.language
cellType int
 regex \-?[0-9]+
cellType assertWord
 highlightScope keyword.operator
cellType controlWord
 highlightScope keyword.control
cellType setupWord
 highlightScope constant.language
cellType parameterWord
 highlightScope variable.parameter
cellType commandWord
 highlightScope variable.function
abstract assertion
 firstCellType assertWord
abstract terminal
abstract testBlock
 catchAllCellType anyWord
 constructors
  nodejs ./TestBlock.js
 nodeTypes
  #setup
 catchAllNodeType commandNode
 firstCellType controlWord
nodeType #setup
 constructors
  nodejs ./TestSetupNode.js
 firstCellType setupWord
 nodeTypes
  async
  require
  static
  %%|
nodeType %%|
 constructors
  nodejs ./SetupConstructorArgNode.js
 description Pass in a multiline string to setup constructor. #todo: rename
 catchAllNodeType anyNode
 firstCellType parameterWord
nodeType %|
 firstCellType parameterWord
 description Pass in a multiline string as a command arg. #todo: rename
 constructors
  nodejs ./CommandArgNode.js
 catchAllNodeType anyNode
nodeType anyNode
 firstCellType anyWord
 catchAllCellType anyWord
 catchAllNodeType anyNode
nodeType commandNode
 constructors
  nodejs ./CommandNode.js
 description Input is an object, and calls some method with an optional array of string args.
 catchAllCellType anyWord
 catchAllNodeType commandNode
 firstCellType commandWord
 nodeTypes
  %|
  assertion
nodeType error
 constructors
  nodejs ErrorNode
  browser ErrorNode
nodeType =# assertion
 description Intake is an array, and checks if the length of array matches expected.
 cells int
 constructors
  nodejs ./AssertLengthNode.js
nodeType =+ assertion
 catchAllCellType anyWord
 description Converts the input to string and see if the string contains the provided string
 constructors
  nodejs ./AssertIncludesNode.js
nodeType =- assertion
 description Converts the input to string and ensure the string does NOT contain the provided string
 catchAllCellType anyWord
 constructors
  nodejs ./AssertDoesNotIncludeNode.js
nodeType == assertion
 description Intake is anything with a toString method, and compares that to provided expected value.
 catchAllCellType anyWord
 constructors
  nodejs ./AssertEqualNode.js
nodeType =| assertion
 description When your expected value is a multiline string.
 catchAllNodeType anyNode
 constructors
  nodejs ./AssertEqualBlockNode.js
nodeType =~ assertion
 description Assert result is of a certain type.
 cells anyWord
 constructors
  nodejs ./AssertTypeOfNode.js
nodeType #! terminal
 cells filepath
 catchAllCellType commentWord
 firstCellType commentWord
nodeType async terminal
 description Set this in setup to test async methods.
nodeType require terminal
 cells filepath
 catchAllCellType anyWord
 firstCellType topWord
nodeType static terminal
 description Add this to setup node to import class directly without initiating it, for static method testing.
nodeType #test testBlock
nodeType +#test testBlock
 constructors
  nodejs ./SoloTestBlock.js
nodeType -#test testBlock
 constructors
  nodejs ./SkippedTestBlock.js