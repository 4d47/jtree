grammar
 name swarm
 description A Tree Language for unit testing.
 nodeTypes
  #!
  #setup
  testBlock
 catchAllNodeType error
 constructors
  nodejs ./SwarmProgramRoot.js
cellType anyWord
 highlightScope string
cellType filepath
 highlightScope string
cellType int
 regex \-?[0-9]+
abstract assertion
 highlightScope keyword.operator
abstract terminal
abstract testBlock
 catchAllCellType anyWord
 constructors
  nodejs ./TestBlock.js
 nodeTypes
  #setup
 catchAllNodeType commandNode
 highlightScope keyword.control
nodeType #setup
 constructors
  nodejs ./TestSetupNode.js
 highlightScope constant.language
 nodeTypes
  async
  require
  static
  %%|
nodeType %%|
 constructors
  nodejs ./SetupConstructorArgNode.js
 description Pass in a multiline string to setup constructor. #todo: rename
 catchAllNodeType any
 highlightScope variable.parameter
nodeType %|
 highlightScope variable.parameter
 description Pass in a multiline string as a command arg. #todo: rename
 constructors
  nodejs ./CommandArgNode.js
 catchAllNodeType any
nodeType any
 catchAllCellType anyWord
 catchAllNodeType any
nodeType commandNode
 constructors
  nodejs ./CommandNode.js
 description Input is an object, and calls some method with an optional array of string args.
 catchAllCellType anyWord
 catchAllNodeType commandNode
 highlightScope variable.function
 nodeTypes
  %|
  assertion
nodeType error
 constructors
  nodejs ErrorNode
  browser ErrorNode
nodeType =# assertion
 description Intake is an array, and checks if the length of array matches expected.
 cells int
 constructors
  nodejs ./AssertLengthNode.js
nodeType =+ assertion
 catchAllCellType anyWord
 description Converts the input to string and see if the string contains the provided string
 constructors
  nodejs ./AssertIncludesNode.js
nodeType =- assertion
 description Converts the input to string and ensure the string does NOT contain the provided string
 catchAllCellType anyWord
 constructors
  nodejs ./AssertDoesNotIncludeNode.js
nodeType == assertion
 description Intake is anything with a toString method, and compares that to provided expected value.
 catchAllCellType anyWord
 constructors
  nodejs ./AssertEqualNode.js
nodeType =| assertion
 description When your expected value is a multiline string.
 catchAllNodeType any
 constructors
  nodejs ./AssertEqualBlockNode.js
nodeType =~ assertion
 description Assert result is of a certain type.
 cells anyWord
 constructors
  nodejs ./AssertTypeOfNode.js
nodeType #! terminal
 cells filepath
 catchAllCellType anyWord
 highlightScope comment
nodeType async terminal
 description Set this in setup to test async methods.
nodeType require terminal
 cells filepath
 catchAllCellType anyWord
 highlightScope constant.language
nodeType static terminal
 description Add this to setup node to import class directly without initiating it, for static method testing.
nodeType #test testBlock
nodeType +#test testBlock
 constructors
  nodejs ./SoloTestBlock.js
nodeType -#test testBlock
 constructors
  nodejs ./SkippedTestBlock.js