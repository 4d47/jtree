grammar
 name swarm
 description A Tree Language for unit testing.
 inScope #! arrangeTestSubject testBlock
 catchAllNodeType error
 constructors
  nodejs ./SwarmLang.js
cellType anyFirstWord
cellType anyWord
 highlightScope string
cellType arrangeWord
 highlightScope constant.language
cellType assertWord
 highlightScope keyword.operator
cellType commandWord
 highlightScope variable.function
cellType commentWord
 highlightScope comment
cellType controlWord
 highlightScope keyword.control
cellType extraWord
 highlightScope invalid
cellType filepath
 highlightScope string
cellType int
 regex \-?[0-9]+
 highlightScope constant.numeric.integer
cellType parameterWord
 highlightScope variable.parameter
cellType typeOfOption
 description The 6 possible results for Javascript's typeof.
 highlightScope constant.language
 enum object boolean function number string undefined
abstract assertion
 firstCellType assertWord
abstract terminal
 firstCellType arrangeWord
abstract testBlock
 catchAllCellType anyWord
 constructors
  nodejs ./SwarmLang.js TestBlock
 inScope arrangeTestSubject
 catchAllNodeType commandNode
 firstCellType controlWord
nodeType #!
 description Standard bash hashbang line.
 cells commentWord
 catchAllCellType commentWord
 firstCellType commentWord
nodeType arrangeTestSubject
 constructors
  nodejs ./SwarmLang.js TestSetupNode
 firstCellType arrangeWord
 inScope async require static constructWithBlockString
nodeType blockStringParam
 firstCellType parameterWord
 description Pass in a multiline string as a command arg. #todo: rename
 constructors
  nodejs ./SwarmLang.js CommandArgNode
 catchAllNodeType textBlock
nodeType commandNode
 constructors
  nodejs ./SwarmLang.js CommandNode
 description Input is an object, and calls some method with an optional array of string args.
 catchAllCellType anyWord
 catchAllNodeType commandNode
 firstCellType commandWord
 inScope blockStringParam assertion
nodeType constructWithBlockString
 constructors
  nodejs ./SwarmLang.js SetupConstructorArgNode
 description Pass in a multiline string to setup constructor. #todo: rename
 catchAllNodeType textBlock
 firstCellType arrangeWord
nodeType error
 baseNodeType errorNode
nodeType textBlock
 firstCellType anyWord
 catchAllCellType anyWord
 catchAllNodeType textBlock
nodeType blockStringIs
 description When your expected value is a multiline string.
 catchAllNodeType textBlock
 constructors
  nodejs ./SwarmLang.js AssertEqualBlockNode
 extends assertion
nodeType lengthIs
 description Intake is an array, and checks if the length of array matches expected.
 cells int
 constructors
  nodejs ./SwarmLang.js AssertLengthNode
 extends assertion
nodeType stringExcludes
 description Converts the input to string and ensure the string does NOT contain the provided string
 catchAllCellType anyWord
 constructors
  nodejs ./SwarmLang.js AssertDoesNotIncludeNode
 extends assertion
nodeType stringIncludes
 catchAllCellType anyWord
 description Converts the input to string and see if the string contains the provided string
 constructors
  nodejs ./SwarmLang.js AssertIncludesNode
 extends assertion
nodeType stringIs
 description Intake is anything with a toString method, and compares that to provided expected value.
 catchAllCellType anyWord
 constructors
  nodejs ./SwarmLang.js AssertEqualNode
 extends assertion
nodeType typeIs
 description Assert result is one of Javascript's 6 typeof types.
 cells typeOfOption
 constructors
  nodejs ./SwarmLang.js AssertTypeOfNode
 extends assertion
nodeType async
 description Set this in setup to test async methods.
 extends terminal
nodeType require
 cells filepath
 catchAllCellType anyWord
 extends terminal
nodeType static
 description Add this to setup node to import class directly without initiating it, for static method testing.
 extends terminal
nodeType skipTest
 description If you want to skip running a test.
 constructors
  nodejs ./SwarmLang.js SkippedTestBlock
 extends testBlock
nodeType test
 description Basic test block.
 extends testBlock
nodeType testOnly
 description If set, only this test block will be run.
 constructors
  nodejs ./SwarmLang.js SoloTestBlock
 extends testBlock