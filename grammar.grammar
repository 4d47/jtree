@grammar grammar
 @description This file is just used for type checking and documenting grammar files. The actual tree grammar parser is hard coded.
 @catchAllKeyword error
 @version 1.0.0
 @keywords
  @grammar
  @keyword
  @wordType
  @abstract
@wordType filepath
 @regex .*
@wordType int
 @regex \-?[0-9]+
 @highlightScope constant.numeric
@wordType word
 @regex [a-zA-Z]+
 @highlightScope variable.parameter
@wordType lowercaseWord
 @regex [a-z]+
@wordType any
 @regex .*
@wordType string
 @regex .*
 @highlightScope string
@wordType filepath
 @regex .*
@wordType keyword
 @regex [^ ]*
 @highlightScope variable.parameter
@wordType regex
 @regex .*
 @highlightScope string.regexp
@wordType semanticVersion
 @regex [0-9]+\.[0-9]+\.[0-9]+
 @highlightScope constant.numeric
@wordType wordType
 @keywordTable @wordType 1
@keyword @grammar
 @required
 @single
 @highlightScope constant.language
 @columns word
 @keywords
  @description
  @keywords
  @constructor
  @compiler
  @catchAllKeyword
  @version
@keyword @version
 @columns semanticVersion
 @highlightScope constant.language
@keyword @abstract @keyword
 @highlightScope entity.name.tag
 @keywords
  keywordRule
  @group
@keyword @wordType
 @columns any*
 @highlightScope variable.parameter
 @keywords
  @highlightScope
  @regex
  @keywordTable
  @enum
  @parseWith
@keyword @parseWith variadicKeywordRule
@keyword @group
 @columns any*
 @highlightScope entity.other.attribute-name
@keyword @enum
 @columns any*
 @highlightScope entity.other.attribute-name
@keyword @keywordTable
 @columns any int
 @highlightScope entity.other.attribute-name
@abstract keywordRule
 @highlightScope entity.other.attribute-name
 @single
@abstract monadicKeywordRule keywordRule
 @columns any
@abstract diadicKeywordRule keywordRule
 @columns any any
@keyword @catchAllKeyword monadicKeywordRule
@keyword @constructor variadicKeywordRule
 @description If specified, the existence of this constructor will be checked when the grammar file is checked for errors.
@keyword @frequency monadicKeywordRule
@abstract variadicKeywordRule keywordRule
 @columns any*
@keyword @columns keywordRule
 @columns wordType*
@keyword @regex variadicKeywordRule
 @columns regex*
@abstract compilerRule
 @columns any*
 @highlightScope entity.other.attribute-name
@keyword @compiler monadicKeywordRule
 @keywords
  @sub
  @listDelimiter
  @openChildren
  @closeChildren
  @indentCharacter
@keyword @sub compilerRule
@keyword @listDelimiter compilerRule
@keyword @openChildren compilerRule
@keyword @closeChildren compilerRule
@keyword @indentCharacter compilerRule
@keyword @description keywordRule
 @columns string*
@abstract ntKeywordRule keywordRule
@keyword defaultTuple
 @columns any any*
@keyword @defaults keywordRule
 @catchAllKeyword defaultTuple
@keyword constantAny
 @columns any*
 @catchAllKeyword constantAny
@keyword constantDefinition
 @columns any*
 @catchAllKeyword constantAny
@keyword @constants keywordRule
 @catchAllKeyword constantDefinition
@keyword keywordUse
@keyword @any keywordRule
@keyword @required keywordRule
@keyword @single keywordRule
@keyword @tags keywordRule
 @columns any*
@keyword @highlightScope keywordRule
 @columns any
@keyword @keywords ntKeywordRule
 @catchAllKeyword keywordUse
@keyword error
 @constructor js ErrorNode
@keyword @keyword
 @columns keyword any*
 @catchAllKeyword error
 @constructor js NonTerminalNode
 @highlightScope variable.function
 @keywords
  keywordRule