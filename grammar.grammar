@grammar grammar
 @description This file is just used for type checking and documenting grammar files. The actual tree grammar parser is hard coded.
 @catchAllKeyword error
 @extensions grammar gram
 @version 1.0.0
 @keywords
  @grammar
  @keyword
  @wordType
  @abstract
@wordType filepath
 @regex .*
@wordType int
 @regex \-?[0-9]+
 @highlightScope constant.numeric
@wordType word
 @regex [a-zA-Z]+
 @highlightScope variable.parameter
@wordType lowercaseWord
 @regex [a-z]+
@wordType any
 @regex .*
@wordType string
 @regex .*
 @highlightScope string
@wordType filepath
 @regex .*
@wordType keyword
 @regex [^ ]*
 @highlightScope variable.parameter
@wordType regex
 @regex .*
 @highlightScope string.regexp
@wordType semanticVersion
 @regex [0-9]+\.[0-9]+\.[0-9]+
 @highlightScope constant.numeric
@wordType wordType
 @keywordTable @wordType 1
@keyword @grammar
 @required
 @single
 @highlightScope constant.language
 @columns word
 @keywords
  @description
  @extensions
  @keywords
  @constructors
  @compiler
  @catchAllKeyword
  @version
@keyword @version
 @columns semanticVersion
 @highlightScope constant.language
@keyword @extensions
 @columns string*
 @highlightScope constant.language
@keyword @abstract @keyword
 @highlightScope entity.name.tag
 @keywords
  _keywordRule
  @group
@keyword @wordType
 @columns any*
 @highlightScope variable.parameter
 @keywords
  @highlightScope
  @regex
  @keywordTable
  @enum
  @parseWith
@keyword @parseWith _variadicKeywordRule
@keyword @group
 @columns any*
 @highlightScope entity.other.attribute-name
@keyword @enum
 @columns any*
 @highlightScope entity.other.attribute-name
@keyword @keywordTable
 @columns any int
 @highlightScope entity.other.attribute-name
@abstract _keywordRule
 @highlightScope entity.other.attribute-name
 @single
@abstract _monadicKeywordRule _keywordRule
 @columns any
@abstract _diadicKeywordRule _keywordRule
 @columns any any
@keyword @catchAllKeyword _monadicKeywordRule
@abstract _constructor _variadicKeywordRule
 @description If specified, the existence of this constructor will be checked when the grammar file is checked for errors.
@keyword @constructors _nonTerminalKeywordRule
 @catchAllKeyword _constructor
@keyword @frequency _monadicKeywordRule
@abstract _variadicKeywordRule _keywordRule
 @columns any*
@keyword @columns _keywordRule
 @columns wordType*
@keyword @regex _variadicKeywordRule
 @columns regex*
@abstract _compilerRule
 @columns any*
 @highlightScope entity.other.attribute-name
@keyword @compiler _monadicKeywordRule
 @keywords
  @sub
  @listDelimiter
  @openChildren
  @closeChildren
  @indentCharacter
@keyword @sub _compilerRule
@keyword @listDelimiter _compilerRule
@keyword @openChildren _compilerRule
@keyword @closeChildren _compilerRule
@keyword @indentCharacter _compilerRule
@keyword @description _keywordRule
 @columns string*
@abstract _nonTerminalKeywordRule _keywordRule
@keyword defaultTuple
 @columns any any*
@keyword @defaults _keywordRule
 @catchAllKeyword defaultTuple
@keyword constantAny
 @columns any*
 @catchAllKeyword constantAny
@keyword constantDefinition
 @columns any*
 @catchAllKeyword constantAny
@keyword @constants _keywordRule
 @catchAllKeyword constantDefinition
@keyword keywordUse
@keyword @any _keywordRule
@keyword @required _keywordRule
@keyword @single _keywordRule
@keyword @tags _keywordRule
 @columns any*
@keyword @highlightScope _keywordRule
 @columns any
@keyword @keywords _nonTerminalKeywordRule
 @catchAllKeyword keywordUse
@keyword error
 @constructors
  nodejs ErrorNode
  browser ErrorNode
@keyword @keyword
 @columns keyword any*
 @catchAllKeyword error
 @constructors
  nodejs NonTerminalNode
  browser NonTerminalNode
 @highlightScope variable.function
 @keywords
  _keywordRule